; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define ApplicationName 'Blink'
#define ApplicationVersion GetFileVersion('..\Source\Blink.Client\bin\Release\Blink.Client.exe')

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId                   = {{1004820E-4DA8-4B9E-B289-3D5FC9FB5C45}
AppName                 = {#ApplicationName}
AppVersion              = {#ApplicationVersion}
AppVerName              = {#ApplicationName} {#ApplicationVersion}
AppPublisher            = Jonathan Búcaro
AppPublisherURL         = http://jonathanbucaro.com
Compression             = lzma
DefaultDirName          = {userappdata}\Blink
DisableDirPage          = yes
DisableWelcomePage      = no
DefaultGroupName        = Blink
DisableProgramGroupPage = yes
OutputBaseFilename      = Blink Setup
PrivilegesRequired      = lowest
SetupIconFile           = Resources\lightbulb.ico
SolidCompression        = yes
UninstallDisplayIcon    = {userappdata}\Blink\Blink.Client.exe
VersionInfoVersion      = {#ApplicationVersion}
WizardImageFile         = Resources\settings.bmp
LicenseFile             = Resources\mit_license.txt

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; Blink 
Source: "..\Source\Blink.Client\bin\Release\Blink.Client.exe";                                                DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Blink.Client.exe.config";                                         DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Blink.Core.dll";                                                  DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Blink.Plugin.dll";                                                DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\CommandLine.dll";                                                 DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\CommandLine.xml";                                                 DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Newtonsoft.Json.dll";                                             DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Newtonsoft.Json.xml";                                             DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Microsoft.WindowsAPICodePack.dll";                                DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Microsoft.WindowsAPICodePack.Shell.dll";                          DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Microsoft.WindowsAPICodePack.xml";                                DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion
Source: "..\Source\Blink.Client\bin\Release\Microsoft.WindowsAPICodePack.Shell.dll";                          DestDir: "{userappdata}\Blink";                                           Flags: replacesameversion

; assets
Source: "Resources\folder.ico";                                                                               DestDir: "{userappdata}\Blink";                                           Flags: ignoreversion
Source: "Resources\spreadsheet.ico";                                                                          DestDir: "{userappdata}\Blink";                                           Flags: ignoreversion
Source: "Resources\lightbulb.ico";                                                                            DestDir: "{userappdata}\Blink";                                           Flags: ignoreversion
Source: "Resources\delete.ico";                                                                               DestDir: "{userappdata}\Blink";                                           Flags: ignoreversion
Source: "Resources\gitlog.ico";                                                                               DestDir: "{userappdata}\Blink";                                           Flags: ignoreversion
Source: "..\Source\Plugins\Blink.Plugin.CommonConfiguration\bin\Release\folder.settings.json";                DestDir: "{userdocs}\Blink\CommonConfiguration";                          Flags: replacesameversion
; Plugins:
; Cleanse
Source: "..\Source\Plugins\Blink.Plugin.Cleanse\bin\Release\Blink.Plugin.Cleanse.dll";                        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Cleanse";              Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Cleanse\bin\Release\icon.ico";                                        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Cleanse";              Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Cleanse\bin\Release\plugin.json";                                     DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Cleanse";              Flags: replacesameversion
; FolderStructure
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\Blink.Plugin.FolderStructure.dll";        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\Blink.Plugin.CommonConfiguration.dll";    DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\folder.settings.json";                    DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\Newtonsoft.Json.dll";                     DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\Newtonsoft.Json.xml";                     DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\icon.ico";                                DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.FolderStructure\bin\Release\plugin.json";                             DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.FolderStructure";      Flags: replacesameversion
; Spreadsheet
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Blink.Plugin.CommonConfiguration.dll";        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Blink.Plugin.Spreadsheet.dll";                DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\EPPlus.dll";                                  DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\EPPlus.Interfaces.dll";                       DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\EPPlus.System.Drawing.dll";                   DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\EPPlus.xml";                                  DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\folder.settings.json";                        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Microsoft.IO.RecyclableMemoryStream.dll";     DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Microsoft.IO.RecyclableMemoryStream.xml";     DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Newtonsoft.Json.dll";                         DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\Newtonsoft.Json.xml";                         DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\icon.ico";                                    DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.Spreadsheet\bin\Release\plugin.json";                                 DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.Spreadsheet";          Flags: replacesameversion
; GitLog
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\Blink.Plugin.GitLog.dll";                          DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\EPPlus.dll";                                       DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\EPPlus.Interfaces.dll";                            DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\EPPlus.System.Drawing.dll";                        DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\EPPlus.xml";                                       DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\LibGit2Sharp.dll";                                 DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\LibGit2Sharp.dll.config";                          DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\LibGit2Sharp.xml";                                 DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\Microsoft.IO.RecyclableMemoryStream.dll";          DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\Microsoft.IO.RecyclableMemoryStream.xml";          DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\icon.ico";                                         DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\plugin.json";                                      DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog";               Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\lib\win32\x64\git2-106a5f2.dll";                   DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog\lib\win32\x64"; Flags: replacesameversion
Source: "..\Source\Plugins\Blink.Plugin.GitLog\bin\Release\lib\win32\x86\git2-106a5f2.dll";                   DestDir: "{userappdata}\Blink\Plugins\Blink.Plugin.GitLog\lib\win32\x86"; Flags: replacesameversion


;[Icons] 
;Name: "{group}\Blink"; Filename: "{userappdata}\Blink\Blink.Client.exe"

[Registry]
; Main menu
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink";                    ValueType: expandsz; ValueName: "MUIVerb";                                                                                                          Flags: deletekey uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink";                    ValueType: expandsz; ValueName: "SubCommands";                                                                                                      Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink";                    ValueType: string  ; ValueName: "Icon";            ValueData: """{userappdata}\Blink\lightbulb.ico""";                                              Flags: uninsdeletekey
; FolderStructure
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd1";         ValueType: dword  ;  ValueName: "AttributeMask";   ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd1";         ValueType: dword  ;  ValueName: "AttributeValue";  ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd1";         ValueType: string ;  ValueName: "Icon";            ValueData: """{userappdata}\Blink\folder.ico""";                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd1";         ValueType: string ;  ValueName: "MUIVerb";         ValueData: "Build folder structure";                                                             Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd1\command"; ValueType: string ;                                ValueData: """{userappdata}\Blink\Blink.Client.exe"" -a folder -d ""%1""";                       Flags: uninsdeletekey
; Spreadsheet
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd2";         ValueType: dword  ;  ValueName: "AttributeMask";   ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd2";         ValueType: dword  ;  ValueName: "AttributeValue";  ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd2";         ValueType: string ;  ValueName: "Icon";            ValueData: """{userappdata}\Blink\spreadsheet.ico""";                                            Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd2";         ValueType: string ;  ValueName: "MUIVerb";         ValueData: "Generate spreadsheet file";                                                          Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd2\command"; ValueType: string ;                                ValueData: """{userappdata}\Blink\Blink.Client.exe"" -a spreadsheet -d ""%1""";                  Flags: uninsdeletekey
; Cleanse
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd3";         ValueType: dword  ;  ValueName: "AttributeMask";   ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd3";         ValueType: dword  ;  ValueName: "AttributeValue";  ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd3";         ValueType: string ;  ValueName: "Icon";            ValueData: """{userappdata}\Blink\delete.ico""";                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd3";         ValueType: string ;  ValueName: "MUIVerb";         ValueData: "Cleanse empty folders";                                                              Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd3\command"; ValueType: string ;                                ValueData: """{userappdata}\Blink\Blink.Client.exe"" -a cleanse -d ""%1""";                      Flags: uninsdeletekey
; GitLog
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4";         ValueType: dword  ;  ValueName: "AttributeMask";   ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4";         ValueType: dword  ;  ValueName: "AttributeValue";  ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4";         ValueType: string ;  ValueName: "Icon";            ValueData: """{userappdata}\Blink\gitlog.ico""";                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4";         ValueType: string ;  ValueName: "MUIVerb";         ValueData: "Git Log";                                                                            Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4\command"; ValueType: string ;                                ValueData: """{userappdata}\Blink\Blink.Client.exe"" -a gitlog -d ""%1""";                       Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd4";         ValueType: dword  ;  ValueName: "commandflags";    ValueData: "$00000040";                                                                          Flags: uninsdeletekey
; Configuration File
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd5";         ValueType: dword  ;  ValueName: "AttributeMask";   ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd5";         ValueType: dword  ;  ValueName: "AttributeValue";  ValueData: "$1";                                                                                 Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd5";         ValueType: string ;  ValueName: "Icon";            ValueData: """{userappdata}\Blink\lightbulb.ico""";                                              Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd5";         ValueType: string ;  ValueName: "MUIVerb";         ValueData: "Open configuration file";                                                            Flags: uninsdeletekey
Root: "HKCU"; Subkey: "Software\Classes\Directory\shell\Blink\shell\cmd5\command"; ValueType: string ;                                ValueData: """explorer.exe"" ""{userdocs}\Blink\CommonConfiguration""";                          Flags: uninsdeletekey
                                                                                                                                                       
[InstallDelete]
; Remove old version of Blink
Type: files; Name: "{userappdata}\Blink\BlinkClient.exe"
Type: files; Name: "{userappdata}\Blink\EPPlus.dll"
Type: files; Name: "{userappdata}\Blink\Newtonsoft.Json.dll"
Type: files; Name: "{userappdata}\Blink\branch.settings.json"
Type: files; Name: "{userappdata}\Blink\BlinkLib.dll"
Type: files; Name: "{userappdata}\Blink\BlinkUtils.dll"

[Code]

procedure InitializeWizard();
begin
  { Welcome page }
  { Hide the labels }
  WizardForm.WelcomeLabel1.Visible := False;
  WizardForm.WelcomeLabel2.Visible := False;
  { Stretch image over whole page }
  WizardForm.WizardBitmapImage.Width := WizardForm.WizardBitmapImage.Parent.Width;
  WizardForm.WizardBitmapImage.Height :=WizardForm.WizardBitmapImage.Parent.Height;

  { Finished page }
  { Hide the labels }
  WizardForm.FinishedLabel.Visible := False;
  WizardForm.FinishedHeadingLabel.Visible := False;
  { Stretch image over whole page }
  WizardForm.WizardBitmapImage2.Width := WizardForm.WizardBitmapImage2.Parent.Width;
  WizardForm.WizardBitmapImage2.Height := WizardForm.WizardBitmapImage2.Parent.Height;
end;